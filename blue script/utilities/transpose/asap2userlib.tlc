%% 
%%  
%% 
%%
%% Abstract:
%%   User ASAP2 library.
%%
%% Copyright 1994-2016 The MathWorks, Inc.
%%
%selectfile NULL_FILE

%% Function: ASAP2UserFcnWriteHead
%% Abstract: Text to include at beginning of ASAP2 file.
%%
%function ASAP2UserFcnWriteFileHead() Output
  /******************************************************************************
   *
   *  ASAP2 file:     %<ASAP2FileName>
   *
   %<LibCommonHeaderInfo()>\
   *
   * (add additional file header information here)
   *
   ******************************************************************************/
%endfunction


%% Function: ASAP2UserFcnWriteHeader
%% Abstract: Define format for HEADER section of ASAP2 file.
%%
%function ASAP2UserFcnWriteHeader() Output
  /begin HEADER "Header Comments go here"
   
  /end HEADER
%endfunction


%% Function: ASAP2UserFcnWriteHardwareInterface
%% Abstract: Used to define hardware interface for ASAP2 file.
%%
%% NOTE: It may be easiest to inline separate files for each section:
%% For example:
%%   %include "./user_a2ml.txt"
%% 
%function ASAP2UserFcnWriteHardwareInterface() Output

     /begin MOD_PAR "%<ASAP2ModParComment>"

     /end MOD_PAR
    
     /begin MOD_COMMON  "%<ASAP2MOD_COMMON_Comment>" 
        BYTE_ORDER     %<ASAP2BYTE_ORDER>
     /end MOD_COMMON
%endfunction

%% Function: ASAP2UserFcnWriteFileTail
%% Abstract: Text to include at end of ASAP2 file.
%%
%function ASAP2UserFcnWriteFileTail() Output
  /* (add any file trailer comments here) */
  /* EOF %<ASAP2FileName> */
%endfunction

%% Function: ASAP2UserFcnASAP2Version
%% Abstract: Defines ASAP2 Version Number.
%%
%%  NOTE: 'VersionNo' represents the main version number and 'UpgradeNo' the 
%%  upgrade number (fractional part of version number).        
%%
%function ASAP2UserFcnASAP2Version() void
  %assign VersionNo = "1"
  %assign UpgradeNo = "31"
  %return [%<VersionNo>, %<UpgradeNo>]
%endfunction

%% Function: ASAP2UserFcnDataTypeAlias
%% Abstract: Maps Simulink DataTypeId to desired data type name.
%%
%function ASAP2UserFcnDataTypeAlias(dtId) void
  %switch dtId
    %case tSS_BOOLEAN
      %return "UBYTE"
    %case tSS_UINT8
      %return "UBYTE"
    %case tSS_INT8
      %return "SBYTE"
    %case tSS_UINT16
      %return "UWORD"
    %case tSS_INT16
      %return "SWORD"
    %case tSS_UINT32
      %return "ULONG"
    %case tSS_INT32
      %return "SLONG"
    %case tSS_SINGLE
      %return "FLOAT32_IEEE"
    %case tSS_DOUBLE
      %return "FLOAT64_IEEE"
    %default
      %if LibIsEnumDataType(dtId)
        %if LibIsEnumTypeStoredAsInt(dtId)
          %return "SLONG"
        %else
          %assign enumStorageId = LibGetEnumTypeStorageType(dtId)
          %return ASAP2UserFcnDataTypeAlias(enumStorageId)
        %endif
      %endif

     %%START_ASSERT
       %assign errTxt = "Unhandled dataType. DataTypeID = %<dtId>"
       %<LibReportError(errTxt)>
     %%END_ASSERT
  %endswitch
%endfunction

%% Function: ASAP2UserFcnRecordLayoutAlias_Common
%% Abstract: Maps Simulink DataTypeId to desired data type name for use in 
%%           record layout
%%
%function ASAP2UserFcnRecordLayoutAlias_Common(dtId) void
  %switch dtId
    %case tSS_UINT8
      %return "UBYTE"
    %case tSS_INT8
      %return "SBYTE"
    %case tSS_UINT16
      %return "UWORD"
    %case tSS_INT16
      %return "SWORD"
    %case tSS_UINT32
      %return "ULONG"
    %case tSS_INT32
      %return "SLONG"
    %case tSS_SINGLE
      %return "FLOAT32_IEEE"
    %case tSS_DOUBLE
      %return "FLOAT64_IEEE"
    %default
     %%START_ASSERT
       %assign errTxt = "Unhandled dataType. DataTypeID = %<dtId>"
       %<LibReportError(errTxt)>
     %%END_ASSERT
  %endswitch
%endfunction

%% Function: ASAP2UserFcnWriteMeasurement
%% Abstract: Defines format for writing out MEASUREMENTs.
%%
%function ASAP2UserFcnWriteMeasurement(signal) Output
  %<LibASAP2AddMeasurementToGraphicalGroup(signal)>
  %assign dtId = LibASAP2GetDataTypeId(signal)
    /begin MEASUREMENT
      /* Name                   */      %<LibASAP2GetSymbol(signal)>
      /* Long identifier        */      "%<LibASAP2GetLongID(signal)>"
      /* Data type              */      %<ASAP2UserFcnDataTypeAlias(dtId)>
      /* Conversion method      */      %<LibASAP2GetCompuMethod(signal)>
      /* Resolution (Not used)  */      0
      /* Accuracy (Not used)    */      0
      /* Lower limit            */      %<LibASAP2GetPhysicalMin(signal)>
      /* Upper limit            */      %<LibASAP2GetPhysicalMax(signal)>
      %if LibGetRecordWidth(signal) > 1
	/* Array Size             */
	ARRAY_SIZE                        %<LibGetRecordWidth(signal)>
      %endif
      ECU_ADDRESS                       %<LibASAP2GetAddress(signal)>
    /end MEASUREMENT
%selectfile ASAP2List
%<LibASAP2GetSymbol(signal)>
%selectfile ModelASAP2File
%endfunction

%% Function: ASAP2UserFcnWriteStructMeasurement
%% Abstract: Defines format for writing out MEASUREMENTs.
%%
%% Arguments:
%%   - signal: Variable name or field name if in a C Structure
%%   - parentName:  Structure name (empty for unstructured data)
%%   - width:  Number of elements in array.
%function ASAP2UserFcnWriteStructMeasurement(signal, parentName, width) Output
  %%
  %% pName is used to append to data name
  %assign pName = ""
  %if ISEMPTY(parentName)
    %assign data = FcnGetGlobalMemoryMapData(signal)
    %assert data.IsStruct
  %else
    %assign data = signal
  %endif
  %% width is only defined for nested array of buses
  %if ISEMPTY(width)
    %assign width = LibGetRecordWidth(signal)
  %endif
  %% The loop is designed to handle both buses and arrays of buses. If signal 
  %% is an array, the index of each element has to be added to the symbol 
  %% string.
  %foreach dataIdx = width
    %%
    %% Get the ROOT or Parent Group
    %if ISEMPTY(parentName)
      %assign rootGroup = LibASAP2CreateGroup("%<::CompiledModel.Name>", ...
        "%<::CompiledModel.Name>", "")
      %if ISFIELD(signal, "GrSrc")
        %assign blkRef  = signal.GrSrc
        %if ( (blkRef[1] != -1) && (blkRef[0] != -1) )
          %assign grSubSys   = ::CompiledModel.BlockHierarchyMap.Subsystem[blkRef[0]]
          %assign parentSysName = FcnConvertSLNameToASAP2Name(grSubSys.Name)
          %assign rootGroup  = LibASAP2CreateGroup(parentSysName, grSubSys.Name, "")
        %endif
      %endif
    %else
      %% Convert bus[idx].elem to bus_idx_elem in order to create valid group name
      %assign pName = SLibMakeGroupName(parentName)
      %assign rootGroup = LibASAP2CreateGroup(pName, parentName, "")
    %endif
    %% Create a Group (with name same as struct name) and make it a sub-Group of
    %% Root Group
    %% Note:
    %%  parentGrpName is the original symbol that may contain "->", ".", and "[idx]" operators
    %%  pGrpName is the modified symbol that forms a valid TLC identifier
    %if width > 1
      %assign parentGrpName = data.Name + "[%<dataIdx>]"
    %else
      %assign parentGrpName = data.Name
    %endif
    %% Append parent name only if the parent is a bus
    %if !ISEMPTY(parentName)
      %assign parentGrpName = parentName + "." + parentGrpName
    %endif
    %% Convert bus[idx].elem to bus_idx_elem in order to create a valid group name
    %% Convert localB->bus to localB_bus for referenced model in order to create a valid group name
    %assign pGrpName = SLibMakeGroupName(parentGrpName)
    %assign parentGrp  = LibASAP2CreateGroup(parentGrpName, "", "")
    %assign tmpVar = LibASAP2AddSubGroupToGroup(rootGroup, parentGrp)
    %%
    %foreach busIdx = data.StructInfo.NumElements  
      %assign dtId = data.StructInfo.BusElement[busIdx].DataTypeIdx
      %assign storageId = LibGetDataTypeStorageIdFromId(dtId)
      %assign dataWidth   =  LibASAP2GetParamWidthForBusElement(data,busIdx)
      %% Nested bus elements is now supported
      %if !LibIsStructDataType(dtId)
        %if width > 1
          %assign measurementName = LibASAP2GetSymbolForBusElement(data,busIdx,dataIdx,parentName)
          %assign measurementAddress = LibASAP2GetAddressForBusElement(data,busIdx,dataIdx,parentName)
        %else
          %assign measurementName = LibASAP2GetSymbolForBusElement(data,busIdx,"",parentName)
          %assign measurementAddress = LibASAP2GetAddressForBusElement(data,busIdx,"",parentName)
        %endif 
        %<LibASAP2AddMeasurementToGroup(parentGrp,measurementName)>
      /begin MEASUREMENT
        /* Name                   */      %<measurementName>
        /* Long identifier        */      "%<LibASAP2GetLongIDForBusElement(data,busIdx)>"
        /* Data type              */      %<ASAP2UserFcnDataTypeAlias(storageId)>
        /* Conversion method      */      %<LibASAP2GetCompuMethodForBusElement(data,busIdx)>
        /* Resolution (Not used)  */      0
        /* Accuracy (Not used)    */      0
        /* Lower limit            */      %<LibASAP2GetPhysicalMinForBusElement(data,busIdx)>
        /* Upper limit            */      %<LibASAP2GetPhysicalMaxForBusElement(data,busIdx)>
        %if dataWidth > 1
	  /* Array Size             */
      ARRAY_SIZE                        %<dataWidth>
        %endif
        ECU_ADDRESS                       %<measurementAddress>
      /end MEASUREMENT
    %selectfile ASAP2List
    %<measurementName>
    %selectfile ModelASAP2File
      %else
        %<ASAP2UserFcnWriteStructMeasurement(data.StructInfo.BusElement[busIdx], parentGrpName, dataWidth)>
      %endif
    %endforeach
  %endforeach
%endfunction

%% Function: ASAP2UserFcnWriteCompuVTab
%% Abstract: Defines format for writing out COMPU_VTab.
%%
%function ASAP2UserFcnWriteCompuVTab(idx) Output
  %assign valuePairs    = LibASAP2GetCompuMethodVTabContents(idx)
  %assign numValuePairs = SIZE(valuePairs, 1)
  /begin COMPU_VTAB
    /* Name of Table          */      VTAB_FOR_%<LibASAP2GetCompuMethodName(idx)>
    /* Long identifier        */      "%<LibASAP2GetCompuMethodLongID(idx)>"
    /* Conversion Type        */      TAB_VERB
    /* Number of Elements     */      %<numValuePairs>
  %foreach pairIdx = numValuePairs
    /* Table Element          */      %<valuePairs[pairIdx]>
  %endforeach
  /end COMPU_VTAB
%endfunction

%% Function: ASAP2UserFcnWriteCompuMethods
%% Abstract: Defines format for writing out COMPU_METHODs.
%%
%function ASAP2UserFcnWriteCompuMethods(idx) Output
  %assign convType = LibASAP2GetCompuMethodConvType(idx)
  %switch convType
  %case "RAT_FUNC"
    /begin COMPU_METHOD
      /* Name of CompuMethod    */      %<LibASAP2GetCompuMethodName(idx)>
      /* Long identifier        */      "%<LibASAP2GetCompuMethodLongID(idx)>"
      /* Conversion Type        */      RAT_FUNC
      /* Format                 */      "%<LibASAP2GetCompuMethodFormat(idx)>"
      /* Units                  */      "%<LibASAP2GetCompuMethodUnits(idx)>"
      /* Coefficients           */      COEFFS %<LibASAP2GetCompuMethodCoeffs(idx)>
    /end COMPU_METHOD
    %break

  %case "TAB_VERB"
    /begin COMPU_METHOD
      /* Name of CompuMethod    */      %<LibASAP2GetCompuMethodName(idx)>
      /* Long identifier        */      "%<LibASAP2GetCompuMethodLongID(idx)>"
      /* Conversion Type        */      TAB_VERB
      /* Format                 */      "%<LibASAP2GetCompuMethodFormat(idx)>"
      /* Units                  */      "%<LibASAP2GetCompuMethodUnits(idx)>"
      /* Conversion Table       */      COMPU_TAB_REF VTAB_FOR_%<LibASAP2GetCompuMethodName(idx)>
    /end COMPU_METHOD
    
    %<ASAP2UserFcnWriteCompuVTab(idx)>
    %break
    
  %default
    %%START_ASSERT
      %assign errTxt = "Unhandled conversion type (%<convType>)"
      %<LibReportError(errTxt)>
    %%END_ASSERT
  %endswitch
%endfunction

%% Function: ASAP2UserFcnWriteComAxis(axisName, param, inputReference, isModelArg)
%% Abstract: 
%%   - Defines format for writing out AXIS_DESCR as COM_AXIS.
%%   - Supports axis parameter that resolves to a Simulink data objects with
%%     a non-Auto storage class as well as those that resolve to model 
%%     arguments in a referenced model
%%  
%% Arguments:
%%   - axisName: Axis name to be used in comment
%%   - param: Axis parameter to write to A2L file as AXIS_DESCR
%%   - inputReference: Reference to input signal
%%   - isModelArg: Is parameter a model argument?
%function ASAP2UserFcnWriteComAxis(axisName, param, inputReference, isModelArg) Output
  %if !isModelArg
    %assign axisCompuMethodName = LibASAP2GetCompuMethod(param)
    %assign axisNumAxisPts = LibASAP2GetNumAxisPts(param)
    %assign axisLowerLimit = LibASAP2GetPhysicalMin(param)
    %assign axisUpperLimit = LibASAP2GetPhysicalMax(param)
    %assign axisRef = LibASAP2GetSymbol(param)
  %else
    %% Axis parameter resolves to a model argument in a referenced model
    %assign dtId = LibGetRecordDataTypeId(param)
    %%
    %assign axisCompuMethodName = FcnASAP2ConstructCompuMethod(dtId, "")
    %assign axisNumAxisPts = LibCGTypeWidth(param.CGTypeIdx)
    %assign axisLowerLimit = LibASAP2GetPhysicalMinFromDTId(dtId)
    %assign axisUpperLimit = LibASAP2GetPhysicalMaxFromDTId(dtId)
    %assign axisRef = param.ModelArgumentIdentifier
  %endif
  %%
      /begin AXIS_DESCR                 
        /* Description of %<axisName> Points */
	/* Axis Type            */      COM_AXIS
	%if ISEMPTY(inputReference)
	  /* Reference to Input   */      NO_INPUT_QUANTITY
	%else
	  /* Reference to Input   */      %<inputReference>
	%endif
	/* Conversion Method    */      %<axisCompuMethodName>
	/* Number of Axis Pts   */      %<axisNumAxisPts>
	/* Lower Limit          */      %<axisLowerLimit>
	/* Upper Limit          */      %<axisUpperLimit>
	AXIS_PTS_REF                    %<axisRef>
    DEPOSIT ABSOLUTE
      /end AXIS_DESCR
  %%    
%endfunction

%% Function: ASAP2UserFcnWriteStdAxis
%% Abstract: Defines format for writing out AXIS_DESCR as STD_AXIS
%%
%function ASAP2UserFcnWriteStdAxis(axisName, param, inputReference) Output
  %% 
  %assign axisInfo      = LibASAP2GetSTDAxisInfo(param)
  %%  
      /begin AXIS_DESCR                 
        /* Description of %<axisName> Points */
	/* Axis Type            */      STD_AXIS
        %if ISEMPTY(inputReference)
	/* Reference to Input   */      NO_INPUT_QUANTITY
	%else
        /* Reference to Input   */      %<inputReference>
        %endif
        /* Conversion Method    */      %<axisInfo.CompuMethod>  
        /* Number of Axis Pts   */      %<axisInfo.NumAxisPoints>         
	/* Lower Limit          */      %<axisInfo.PhysicalMin>  
	/* Upper Limit          */      %<axisInfo.PhysicalMax>  
      /end AXIS_DESCR
  %%    
%endfunction 

%% Function: ASAP2UserFcnWriteFixAxis 
%% Abstract: Defines format for writing out AXIS_DESCR as FIX_AXIS.
%%
%function ASAP2UserFcnWriteFixAxis(axisName, param, inputReference) Output
  %assign axisValue     = param.Value  
  %%  
  %% Compute AXIS_DESCR characteristics for the FIX_AXIS
  %assign numAxisPoints = %<SIZE(axisValue,1)>
  %assign lowerLimit    = CAST("Number", %<axisValue[0]>)
  %assign upperLimit    = CAST("Number", %<axisValue[numAxisPoints-1]>)
  %assign distance      = CAST("Number", %<axisValue[1]-axisValue[0]>)
  %assign axisInfo      = LibASAP2GetFixAxisInfo(param)
  %%
  %% Compute if distance is a power of 2  
  %if (distance > 1) && ((distance & (distance-1)) == 0)
    %% If distance is a power of 2 we write FIX_AXIS_PAR
    %assign shift         = CAST("Number",FEVAL("eval","log2(%<distance>)")) 
    %assign fixAxisParStr = "FIX_AXIS_PAR"
    %assign fixAxisParVal = "     %<lowerLimit> %<shift> %<numAxisPoints>"    
  %else
    %assign fixAxisParStr = "FIX_AXIS_PAR_DIST"
    %assign fixAxisParVal = "%<lowerLimit> %<distance> %<numAxisPoints>"
  %endif
  %%  
      /begin AXIS_DESCR                 
        /* Description of %<axisName> Points */
        /* Axis Type            */      FIX_AXIS
	%if ISEMPTY(inputReference)
	  /* Reference to Input   */      NO_INPUT_QUANTITY
	%else
	  /* Reference to Input   */      %<inputReference>
	%endif
	/* Conversion Method    */      %<axisInfo.CompuMethod>
	/* Number of Axis Pts   */      %<numAxisPoints>
	/* Lower Limit          */      %<axisInfo.PhysicalMin>
	/* Upper Limit          */      %<axisInfo.PhysicalMax>
	FORMAT                          "%<axisInfo.Format>"
    %<fixAxisParStr>               %<fixAxisParVal>
      /end AXIS_DESCR 
%endfunction 

%% Function: ASAP2UserFcnWriteFixAxisList
%% Abstract: Defines format for writing out AXIS_DESCR as FIX_AXIS_PAR_LIST.
%%
%function ASAP2UserFcnWriteFixAxisList(axisName, param, inputReference) Output
  %assign axisValue     = param.Value
  %% Compute AXIS_DESCR characteristics for the FIX_AXIS
  %assign numAxisPoints = %<SIZE(axisValue,1)>
  %assign lowerLimit    = CAST("Number", %<axisValue[0]>)
  %assign upperLimit    = CAST("Number", %<axisValue[numAxisPoints-1]>)
  %%  
  %assign axisInfo      = LibASAP2GetFixAxisInfo(param)
  %%  
      /begin AXIS_DESCR                 
        /* Description of %<axisName> Points */
        /* Axis Type            */      FIX_AXIS
	%if ISEMPTY(inputReference)
	  /* Reference to Input   */      NO_INPUT_QUANTITY
	%else
	  /* Reference to Input   */      %<inputReference>
	%endif
	/* Conversion Method    */      %<axisInfo.CompuMethod>
	/* Number of Axis Pts   */      %<numAxisPoints>
	/* Lower Limit          */      %<axisInfo.PhysicalMin>
	/* Upper Limit          */      %<axisInfo.PhysicalMax>
	FORMAT                          "%<axisInfo.Format>"
        %openfile axisValues
        %foreach jj = numAxisPoints
           %<CAST("Number", axisValue[jj])> \
        %endforeach
        %closefile axisValues
        %assign listValue = FEVAL("regexprep", axisValues, "\\s\*", " ")
        /begin FIX_AXIS_PAR_LIST
        %<listValue>
        /end FIX_AXIS_PAR_LIST
      /end AXIS_DESCR 
%endfunction 


%% [EOF] asap2userlib.tlc
